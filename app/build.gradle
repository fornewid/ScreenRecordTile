apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'
    defaultConfig {
        applicationId 'soup.tile.screenrecord'
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 9
        versionName '0.5.0'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Define all keys for preferences

        def prefUseAudio = "use_audio"
        buildConfigField 'String', 'PREF_USE_AUDIO', "\"${prefUseAudio}\""
        resValue("string", "pref_use_audio", prefUseAudio)

        def prefReturnToSettings = "return_to_settings"
        buildConfigField 'String', 'PREF_RETURN_TO_SETTINGS', "\"${prefReturnToSettings}\""
        resValue("string", "pref_return_to_settings", prefReturnToSettings)
    }
    signingConfigs {
        release {
            def useReleaseKeystore = rootProject.file("signing/app-release.jks").exists()
            if (useReleaseKeystore) {
                storeFile rootProject.file("signing/app-release.jks")
                storePassword propOrDef('storePassword', "")
                keyAlias propOrDef('keyAlias', "")
                keyPassword propOrDef('keyPassword', "")
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [crashlytics_enabled: false]
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [crashlytics_enabled: true]
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.google.firebase:firebase-analytics:18.0.2'
    implementation 'com.google.firebase:firebase-crashlytics:17.3.1'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

if (file("google-services.json").exists()) {
    apply plugin: 'com.google.gms.google-services'
}
