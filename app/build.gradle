apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'
    defaultConfig {
        applicationId 'soup.tile.screenrecord'
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 5
        versionName '0.3.1'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Define all keys for preferences

        def prefStartRecord = "start_record"
        buildConfigField 'String', 'PREF_START_RECORD', "\"${prefStartRecord}\""
        resValue("string", "pref_start_record", prefStartRecord)

        def prefUseAudio = "use_audio"
        buildConfigField 'String', 'PREF_USE_AUDIO', "\"${prefUseAudio}\""
        resValue("string", "pref_use_audio", prefUseAudio)

        def prefCurrentVersion = "current_version"
        buildConfigField 'String', 'PREF_CURRENT_VERSION', "\"${prefCurrentVersion}\""
        resValue("string", "pref_current_version", prefCurrentVersion)
    }
    signingConfigs {
        release {
            def useReleaseKeystore = rootProject.file("signing/app-release.jks").exists()
            if (useReleaseKeystore) {
                storeFile rootProject.file("signing/app-release.jks")
                storePassword propOrDef('storePassword', "")
                keyAlias propOrDef('keyAlias', "")
                keyPassword propOrDef('keyPassword', "")
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [crashlytics_enabled: false]
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [crashlytics_enabled: true]
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.fragment:fragment:1.2.2'
    implementation 'androidx.preference:preference:1.1.0'

    implementation 'com.google.firebase:firebase-analytics:17.2.2'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    implementation 'com.jakewharton.timber:timber:4.7.1'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

if (file("google-services.json").exists()) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'io.fabric'
}
